
_BOM                  : "\uFFFE" | "\uFEFF"

_WS                   : "\t" | " "

_NEWLINE              : "\n" | "\r\n"

_BLANKLINE            : _WS+ _NEWLINE
                      | _NEWLINE

AUTHOR_OR_AFFILIATION : ("a".."z" | "A".."Z" | "." | "," | "-" | "(" | ")" | " ")+

ORGANISATION          : "Internet Engineering Task Force (IETF)"

CATEGORY              : "Standards Track"

DAY                   : "1"

MONTH                 : "January"
                      | "February"
                      | "March"
                      | "April"
                      | "May"
                      | "June"
                      | "July"
                      | "August"
                      | "September"
                      | "October"
                      | "November"
                      | "December"

YEAR                  : ("0".."9")+

author_or_affiliation : AUTHOR_OR_AFFILIATION

organisation          : ORGANISATION 

header_group          : organisation _WS+ author_or_affiliation _NEWLINE

rfc_num               : "Request for Comments:" _WS INT

header_rfc_num        : rfc_num  _WS+ author_or_affiliation _NEWLINE

category              : "Category:" _WS CATEGORY

header_category       : category _WS+ author_or_affiliation _NEWLINE

header_issn           : "ISSN: 2070-1721" _WS+ author_or_affiliation _NEWLINE

header_author         : _WS+ author_or_affiliation _NEWLINE

header_day            : DAY

header_month          : MONTH

header_year           : YEAR 

header_date           : _WS+ (header_day _WS)? header_month _WS header_year _NEWLINE

header_date_author    : header_date
                      | header_author header_date_author

header_rfc            : header_group header_rfc_num header_category header_issn header_date_author

header_block          : header_rfc _BLANKLINE+

TITLE                 : ("a".."z" | "A".."Z" | ":" | "-" | " ")+

title                 : _WS+ TITLE _NEWLINE _BLANKLINE+

ALPHANUM               : /\w/+

NUMBER                : ("0".."9")+

PUNCTUATION           : ("." | "," | ":" | ";" | "+" | "-" | "(" | ")" | "{" | "}" | "^" | "/" | "*" | "?" | "@" | "|" | ">" | "<" | "'" | "\"" | "_" | "[" | "]" | "=" | "!" | "~" | "&")+

TEXT                  : (ALPHANUM | PUNCTUATION | " ")+ 

TEXTLINE              : "   " TEXT _NEWLINE

t                     : TEXTLINE+ _BLANKLINE+

abstract              : "Abstract"            _NEWLINE _BLANKLINE t+

status_of_this_memo   : "Status of This Memo" _NEWLINE _BLANKLINE t+

copyright             : "Copyright Notice"    _NEWLINE _BLANKLINE t+

table_of_contents     : "Table of Contents"   _NEWLINE _BLANKLINE t+

front                 : _BOM _BLANKLINE+ header_block title abstract status_of_this_memo copyright table_of_contents



li_head               : "   *" _WS+ TEXT _NEWLINE

li_cont               : TEXTLINE+ 

li                    : li_head li_cont* _BLANKLINE+

ul                    : li+

section_header        : (NUMBER ".")+ _WS+ TEXT _NEWLINE _BLANKLINE

section_body          : (t | ul)+

section               : section_header section_body

middle                : section+



ref_header            : NUMBER "." _WS+ "References" _NEWLINE _BLANKLINE

REF_ID                : (ALPHANUM | "-")+

ref_label             : "[" REF_ID "]" _NEWLINE?

REF_TEXT              : /[^[]/ (ALPHANUM | PUNCTUATION | " ")+ 

ref_line              : _WS+ REF_TEXT _NEWLINE 

ref_text              : ref_line+ (_BLANKLINE ref_line)? _BLANKLINE

ref                   : _WS+ ref_label ref_text

nref_header           : (NUMBER ".")+ _WS+ "Normative References" _NEWLINE _BLANKLINE

nrefs                 : nref_header ref+

iref_header           : (NUMBER ".")+ _WS+ "Informative References" _NEWLINE _BLANKLINE

irefs                 : iref_header ref+

references            : ref_header nrefs* irefs*



appendix_header_top   : "Appendix " "A".."Z" "." _WS+ TEXT _NEWLINE

appendix_header_sub   : "A".."Z" "." (NUMBER ".")+  _WS+ TEXT _NEWLINE

appendix_header       : appendix_header_top _BLANKLINE
                      | appendix_header_sub _BLANKLINE

appendix              : appendix_header section_body



contributors_header   : "Contributors" _NEWLINE _BLANKLINE

contributors          : contributors_header section_body


authors_header        : "Authors' Addresses" _NEWLINE _BLANKLINE

author_name           : _WS+ TEXT

author_role           : " (" TEXT ")"

author_affiliation    : _WS+ TEXT _NEWLINE

author_email          : _WS+ "Email: " TEXT _NEWLINE

authors_body          : author_name author_role? _NEWLINE author_affiliation _BLANKLINE? author_email _BLANKLINE*

authors               : authors_header authors_body+


back                  : references appendix* contributors? authors


ANYTHING              : (/./ | _NEWLINE)+ 

rfc                   : front middle back

%import common.INT

